plugins {
    id("java")
    id("groovy")
    id("application")
    id("com.github.rahulsom.waena.root").version("0.3.1")
    id("com.github.rahulsom.waena.published").version("0.3.1")
}

apply from: 'gradle/codegen.gradle'

group = 'com.github.rahulsom'
description = "Meaningful Use Validator based on NIST's own."

mainClassName = "gov.nist.mu.validation.Validator"

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'nist/generated/src/java'
        }
        resources {
            srcDir 'src/main/resources'
            srcDir 'build/generated/resources/cachefileDir'
            srcDir 'build/generated/resources/stylesheetDir'
        }
    }
}

repositories {
    mavenCentral()
}

Map<String, String> xmlbeans(String subproject = null) {
    [group: 'org.apache.xmlbeans', name: subproject ? "xmlbeans-$subproject" : 'xmlbeans', version: '2.4.0']
}

Map<String, String> saxon(String subproject = null) {
    [group: 'net.sourceforge.saxon', name: 'saxon', version: '9.1.0.8', classifier: subproject]
}

dependencies {

    implementation 'commons-cli:commons-cli:1.5.0'
    implementation 'commons-lang:commons-lang:2.6'
    implementation 'javax.xml.bind:jsr173_api:1.0'
    implementation 'org.dspace.xmlui.xml:xml-commons-resolver:1.1'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'com.sun.xml.bind:jaxb-core:3.0.2'
    implementation 'com.sun.xml.bind:jaxb-impl:2.3.5'

    implementation saxon()
    implementation saxon('dom')
    implementation saxon('dom4j')
    implementation saxon('jdom')
    implementation saxon('s9api')
    implementation saxon('sql')
    implementation saxon('xom')
    implementation saxon('xqj')

    implementation xmlbeans()
    implementation(xmlbeans('xpath')) {
        exclude group: 'net.sf.saxon'
    }
    implementation xmlbeans('qname')
    implementation xmlbeans('xmlpublic')

    testImplementation 'org.codehaus.groovy:groovy:3.0.9'
    testImplementation "org.spockframework:spock-core:2.0-groovy-3.0"
    testImplementation 'javax.activation:activation:1.1.1'
}

test {
  useJUnitPlatform()
}

contacts {
    validateEmails = true
    'rahulsom@noreply.github.com' {
        moniker("Rahul Somasunderam")
        roles("owner")
        github("https://github.com/rahulsom")
    }
}

println """
Version: $version
"""
