buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.2"
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id "com.bmuschko.nexus" version "2.3.1"
}

apply from: 'gradle/codegen.gradle'
apply from: 'gradle/publishing.gradle'
apply plugin: 'io.codearte.nexus-staging'

group = 'com.github.rahulsom' // This could become 'gov.nist'
version = '0.2'

mainClassName = "gov.nist.mu.validation.Validator"

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'nist/generated/src/java'
        }
        resources {
            srcDir 'src/main/resources'
            srcDir 'build/generated/resources'
        }
    }
}

repositories {
    jcenter()
}

Map<String, String> xmlbeans(String subproject = null) {
    [group: 'org.apache.xmlbeans', name: subproject ? "xmlbeans-$subproject" : 'xmlbeans', version: '2.4.0']
}

Map<String, String> saxon(String subproject = null) {
    [group: 'net.sourceforge.saxon', name: 'saxon', version: '9.1.0.8', classifier: subproject]
}

dependencies {

    implementation 'commons-cli:commons-cli:1.4'
    implementation 'commons-lang:commons-lang:2.6'
    implementation 'javax.xml.bind:jsr173_api:1.0'
    implementation 'org.dspace.xmlui.xml:xml-commons-resolver:1.1'

    implementation saxon()
    implementation saxon('dom')
    implementation saxon('dom4j')
    implementation saxon('jdom')
    implementation saxon('s9api')
    implementation saxon('sql')
    implementation saxon('xom')
    implementation saxon('xqj')

    implementation xmlbeans()
    implementation(xmlbeans('xpath')) {
        exclude group: 'net.sf.saxon'
    }
    implementation xmlbeans('qname')
    implementation xmlbeans('xmlpublic')

    testImplementation 'org.codehaus.groovy:groovy-all:2.5.9'
    testImplementation "org.spockframework:spock-core:1.3-groovy-2.5"
}
